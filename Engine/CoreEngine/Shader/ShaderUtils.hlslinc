float4 Mask_R(float InColor)
{
	return float4(InColor, 0, 0, 1);
};

float4 Mask_G(float InColor)
{
	return float4(0, InColor, 0, 1);
};

float4 Mask_B(float InColor)
{
	return float4(0, 0, InColor, 1);
};

float4 Mask_A(float InColor)
{
	return float4(0, 0, 0, InColor);
};

float4 Mask_RGB(float InColor)
{
	return float4(InColor, InColor, InColor, 1);
};

/**
 * 버퍼로부터 본의 변환 행렬을 가져온다.
 */
float4x4 FetchBoneTransform(uint Bone, StructuredBuffer<float4> BoneTransforms)
{
	Bone *= 4;	// 4x4 행렬이므로 4를 곱해준다.
	float4 row1 = BoneTransforms.Load(Bone + 0);	// 4x4 행렬의 첫번째 행
	float4 row2 = BoneTransforms.Load(Bone + 1);	// 4x4 행렬의 두번째 행
	float4 row3 = BoneTransforms.Load(Bone + 2);	// 4x4 행렬의 세번째 행
	float4 row4 = BoneTransforms.Load(Bone + 3);	// 4x4 행렬의 네번째 행

	float4x4 Matrix = float4x4(row1, row2, row3, row4);	// 4x4 행렬 생성
	return Matrix;
}

/**
 * @brief 텍스처를 일정 속도로 x, y축으로 이동시키는 함수 (애니메이션 효과)
 * @param InTime : 게임 시간 (Delta Time 누적)
 */
float2 Panner(float2 InUV, float2 InSpeed, float InTime)
{
	// 시간 기반으로 Speed에 따라 TexCoord 좌표를 이동시킨다.
	float2 uvOffset = InUV + InSpeed * InTime;

	// frac을 이용하여 0~1 사이의 값으로 정규화한다.
	uvOffset.x = frac(uvOffset.x);
	uvOffset.y = frac(uvOffset.y);

	return uvOffset;
};

float WorldAlignedBlend(float3 InPixelNormal, float InputBlendSharpness, float InputBlendBias)
{
	// Z 축을 기준으로 법선과의 각도를 측정
	float blendFactor = dot(InPixelNormal, float3(0, -1, 0));

	// blendFactor를 0.5로 스케일링하고 Offset을 적용
	blendFactor = blendFactor * 0.5f + 0.5f;

	blendFactor = blendFactor * InputBlendSharpness;

	// BlendSharpness와 BlendBias를 사용해 값 조정
	blendFactor = blendFactor + (InputBlendBias - (InputBlendSharpness * 0.5f));


	// 결과값을 0과 1 사이로 clamp
	return clamp(blendFactor, 0.0f, 1.0f);
}

float3 ComputePointLight(float3      InWorldPos,
						 float3      InWorldNormal,
						 FPointLight InPointLight)
{
	// 빛의 방향을 계산
	float3 lightDir           = InPointLight.Position - InWorldPos;
	float3 lightDirNormalized = normalize(InPointLight.Position - InWorldPos);

	// 빛의 거리를 계산
	float lightDistance = length(lightDir);

	if (lightDistance > InPointLight.Range)
	{
		return float3(0, 0, 0);
	}

	// 빛의 강도를 계산
	float lightIntensity = saturate(1.0f - lightDistance / InPointLight.Range) * InPointLight.Intensity;

	// 빛의 강도를 계산
	float lightFactor = saturate(dot(InWorldNormal, lightDirNormalized));


	// 빛의 강도를 계산
	float3 lightColor = InPointLight.Color * lightIntensity * lightFactor;

	return lightColor;
}

float3 ComputeSpotLight(float3     InWorldPos,
						float3     InWorldNormal,
						FSpotLight InSpotLight)
{
	// 광원으로부터 표면까지의 벡터
	float3 LightDir = -normalize(InSpotLight.Position - InWorldPos);

	// 광원 방향과 표면 법선 간의 내적 계산 (Diffuse 조명)
	float DiffuseFactor = max(dot(InWorldNormal, LightDir), 0.0);

	// 표면이 SpotLight 조사각 내에 있는지 확인
	float SpotEffect = dot(LightDir, -normalize(InSpotLight.Direction)); // 광원 방향과 표면 방향 비교
	SpotEffect       = smoothstep(cos(radians(InSpotLight.SpotAngle)), 1.0, SpotEffect); // 부드러운 경계 처리

	// 광원의 범위(Range) 기반 감쇠
	float Distance         = length(InSpotLight.Position - InWorldPos);
	float RangeAttenuation = saturate(1.0 - (Distance / InSpotLight.Range)); // 선형 감쇠

	// 조명 색과 강도(Intensity) 적용
	float3 LightColor = InSpotLight.Color * InSpotLight.Intensity;

	// 최종 조명 계산
	float3 FinalLight = LightColor * DiffuseFactor * SpotEffect * RangeAttenuation;

	return FinalLight;
}

/**
 * 높이맵을 사용하여 표면의 깊이감을 표현하는 함수
 * @param uv 텍스처 좌표
 * @param viewDir  TBN 공간에서의 뷰 벡터
 * @param heightMap 높이 정보를 저장한 텍스처
 * @param heightRatio 높이 맵의 깊이를 얼마나 확대할지 결정하는 비율
 * @param minSteps 레이 행렬의 최소 샘플링 횟수
 * @param maxSteps 레이 행렬의 최대 샘플링 횟수
 * @param heightMapChannel 높이 맵 데이터가 포함된 텍스처의 채널 (예: R, G, B 중 선택)
 * @return 
 */
float2 ParallaxOcclusionMapping(
	float2 uv,
	float3 viewDir,
	float4 heightMap,
	float  heightRatio,
	int    minSteps,
	int    maxSteps,
	float  heightMapChannel // R: 0, G: 1, B: 2
	)
{
	/// TBN 공간에서의 뷰 벡터를 기반으로 UV 이동 방향 계산
	float2 parallaxDir = normalize(viewDir.xy) * heightRatio;

	// 초기 값 설정
	float layerDepth   = 1.0;
	float currentDepth = 0.0;

	// UV 이동 크기 계산
	float2 deltaUV   = parallaxDir / maxSteps;
	float2 currentUV = uv;

	// 초기 빠른 샘플링: minSteps
	for (int i = 0; i < minSteps; ++i)
	{
		currentDepth += 1.0 / maxSteps;
		currentUV += deltaUV;
	}

	// 세부 탐색: maxSteps - minSteps
	float heightSample;
	for (int i = minSteps; i < maxSteps; ++i)
	{
		heightSample = heightMap[heightMapChannel];

		if (currentDepth < heightSample)
		{
			currentDepth += 1.0 / maxSteps;
			currentUV += deltaUV;
		}
		else
		{
			break;
		}
	}

	// 보간 UV 계산
	float2 prevUV     = currentUV - deltaUV;
	float  prevHeight = heightMap[heightMapChannel];

	float weight = (heightSample - currentDepth) / (heightSample - prevHeight + 0.0001);
	currentUV    = lerp(prevUV, currentUV, saturate(weight));

	return currentUV;
}

float3 BlendAngleCorrectNormal(float3 InBaseNormal, float3 InAdditionalNormal)
{
	// 입력 벡터 정규화
	InBaseNormal       = normalize(InBaseNormal);
	InAdditionalNormal = normalize(InAdditionalNormal);

	// Z 성분 추출
	float3 T1 = float3(InBaseNormal.xy, 0); // N1의 XY 성분만 사용
	float3 T2 = float3(InAdditionalNormal.xy, 0); // N2의 XY 성분만 사용

	// XY 평면에서 내적 계산
	float dotProduct = dot(T1, T2);

	// 보정 값 계산
	float3 corrected = T1 - T2 * dotProduct;

	// 보정된 노멀 벡터
	return normalize(float3(corrected.xy, InBaseNormal.z + InAdditionalNormal.z));
}

float4 WorldAlignedTexture(
	Texture2D    InTexture,
	SamplerState InSampler,
	float3       InWorldPos,
	float3       InWorldNormal,
	float        InTextureSize)
{
	// 월드 노멀의 절대값으로 축 선택 기준 계산
	float3 absNormal   = abs(InWorldNormal);
	float  totalWeight = absNormal.x + absNormal.y + absNormal.z;

	// 각 축의 텍스처 좌표 계산
	float2 uvX = InWorldPos.yz / InTextureSize;
	float2 uvY = InWorldPos.xz / InTextureSize;
	float2 uvZ = InWorldPos.xy / InTextureSize;

	// 각 축에서 텍스처 샘플링 및 가중치 적용
	float4 texX = InTexture.Sample(InSampler, uvX) * (absNormal.x / totalWeight);
	float4 texY = InTexture.Sample(InSampler, uvY) * (absNormal.y / totalWeight);
	float4 texZ = InTexture.Sample(InSampler, uvZ) * (absNormal.z / totalWeight);

	// 혼합하여 최종 텍스처 값 반환
	return texX + texY + texZ;
}


/**
 * 미세면 분산 함수
 */
float DistributionGGX(float3 N, float3 H, float roughness)
{
	float a      = roughness * roughness;
	float a2     = a * a;
	float NdotH  = max(dot(N, H), 0.0);
	float NdotH2 = NdotH * NdotH;

	float nom   = a2;
	float denom = (NdotH2 * (a2 - 1.0) + 1.0);
	denom       = 3.14159 * denom * denom;

	return nom / denom;
}

float3 F_Schlick(float3 F0, float cosTheta)
{
	return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
}

float GeometrySmith(float3 N, float3 V, float3 L, float roughness)
{
	float NdotV = max(dot(N, V), 0.0);
	float NdotL = max(dot(N, L), 0.0);
	float k     = (roughness + 1.0) * (roughness + 1.0) / 8.0;

	float G1_V = NdotV / (NdotV * (1.0 - k) + k);
	float G1_L = NdotL / (NdotL * (1.0 - k) + k);

	return G1_V * G1_L;
}

float3 SpecularGGX(float3 N, float3 V, float3 L, float roughness, float3 F0)
{
	float3 H = normalize(V + L);
	float  D = DistributionGGX(N, H, roughness);
	float  G = GeometrySmith(N, V, L, roughness);
	float3 F = F_Schlick(F0, max(dot(H, V), 0.0));

	return (D * G * F) / (4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.001);
}


float3 ComputePBR(float3     baseColor,    // Base Color
				  float      metallic,      // Metallic
				  float      roughness,     // Roughness
				  float      specular,      // Specular
				  float      ao,            // Ambient AO
				  float3     N,            // Normal (in world space)
				  float3     V,            // View direction (normalized)
				  float3     L,            // Light direction (normalized)
				  float3     lightColor,   // Light color
				  out float3 albedo    // Output diffuse term for debugging
)
{
	const float PI = 3.14159265359;

	// Roughness 처리
	roughness   = clamp(roughness, 0.04, 1.0);
	float alpha = roughness * roughness;

	// Halfway 벡터 계산
	float3 H = normalize(V + L);

	// Fresnel 계산
	float3 F0 = lerp(float3(specular, specular, specular), baseColor, metallic);
	float3 F  = F0 + (1.0 - F0) * pow(1.0 - dot(H, V), 5.0);

	// GGX Geometry Function
	float NdotV = max(dot(N, V), 0.0);
	float NdotL = max(dot(N, L), 0.0);
	float NdotH = max(dot(N, H), 0.0);

	float G1 = NdotV / (NdotV * (1.0 - alpha) + alpha);
	float G2 = NdotL / (NdotL * (1.0 - alpha) + alpha);
	float G  = G1 * G2;

	// GGX Normal Distribution Function
	float D = alpha / (PI * pow(NdotH * NdotH * (alpha - 1.0) + 1.0, 2.0));

	// Specular BRDF
	float3 specularBRDF = (F * G * D) / (4.0 * NdotV * NdotL + 0.001);

	// Diffuse BRDF
	float3 diffuseBRDF = (1.0 - F) * baseColor * (1.0 - metallic) / PI;

	// Ambient AO 적용
	diffuseBRDF *= ao;
	specularBRDF *= ao;

	// 최종 조합
	albedo          = diffuseBRDF * NdotL;
	float3 lighting = (diffuseBRDF + specularBRDF) * NdotL * lightColor;

	return lighting;
}
